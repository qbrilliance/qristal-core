set(source_files
  src/async_executor.cpp
  src/utils.cpp
  src/session.cpp
  src/pretranspiler.cpp
  src/profiler.cpp
  #src/qbTketNoiseAwarePlacement.cpp
  src/QuantumBrillianceRemoteAccelerator.cpp
  src/thread_pool.cpp
  src/optimization/qaoa/qaoa_base.cpp
  src/optimization/qaoa/qaoa_simple.cpp
  src/optimization/qaoa/qaoa_validators.cpp
  # vqee is header only atm
  # We need these in order to complete the linking
  # TODO: refactor session.hpp to no longer contain getters/setters declarations...
  python_module/core/session_getter_setter.cpp
  python_module/core/session_py_help_strings.cpp
)

set(headers
  include/qb/core/qblib.inc
  include/qb/core/qpu_config.json
  include/qb/core/async_executor.hpp
  include/qb/core/cmake_variables.hpp
  include/qb/core/circuit_builder.hpp
  include/qb/core/circuit_builders/exponent.hpp
  include/qb/core/circuit_builders/ry_encoding.hpp
  include/qb/core/session.hpp
  include/qb/core/utils.hpp
  include/qb/core/pretranspiler.hpp
  include/qb/core/profiler.hpp
  include/qb/core/QuantumBrillianceAccelerator.hpp
  include/qb/core/QuantumBrillianceRemoteAccelerator.hpp
  include/qb/core/QuantumBrillianceRemoteVisitor.hpp
  include/qb/core/QuantumBrillianceVisitor.hpp
  include/qb/core/remote_async_accelerator.hpp
  include/qb/core/thread_pool.hpp
  include/qb/core/typedefs.hpp
  include/qb/core/optimization/vqee/vqee.hpp
  include/qb/core/optimization/qaoa/qaoa.hpp
)

# Lightweight header-only interface target
add_library(lightweight_${PROJECT_NAME} INTERFACE ${headers})
add_library(${NAMESPACE}::lightweight_${PROJECT_NAME} ALIAS lightweight_${PROJECT_NAME})

target_include_directories(lightweight_${PROJECT_NAME}
  INTERFACE
    $<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}/include>
    $<INSTALL_INTERFACE:include>
)

target_link_libraries(lightweight_${PROJECT_NAME}
  INTERFACE
    qb::core::noise
    xacc::xacc
    xacc::quantum_gate
    nlohmann::json
    # The following are not strictly needed, but save dependent packages locating them themselves.
    taywee::args
    Eigen3::Eigen
    Python::Python
    pybind11::pybind11
    GTest::gtest
    GTest::gtest_main
)

# Install the library
install(
  TARGETS lightweight_${PROJECT_NAME}
  DESTINATION ${CMAKE_INSTALL_PREFIX}/lib
  EXPORT lightweight_${PROJECT_NAME}Targets
)

# Install the Targets.cmake file for the library
set(cppTargetsFile "lightweight_${PROJECT_NAME}Targets.cmake")
install(
  EXPORT lightweight_${PROJECT_NAME}Targets
  FILE ${cppTargetsFile}
  DESTINATION "${CMAKE_INSTALL_PREFIX}/cmake"
  NAMESPACE ${NAMESPACE}::
)


# Compiled core library
if (NOT SUPPORT_EMULATOR_BUILD_ONLY)

  add_library(${PROJECT_NAME} SHARED ${source_files} ${headers})
  add_library(${NAMESPACE}::${PROJECT_NAME} ALIAS ${PROJECT_NAME})

  set_target_properties(${PROJECT_NAME}
    PROPERTIES
      VERSION ${PROJECT_VERSION}
      FRAMEWORK TRUE
      INSTALL_RPATH "$ORIGIN;${CMAKE_INSTALL_RPATH};${XACC_ROOT}/lib"
  )

  # Include dependencies.
  target_include_directories(${PROJECT_NAME}
    PUBLIC
      $<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}/include>
      $<INSTALL_INTERFACE:include>
  )

  # Link dependencies
  target_link_libraries(${PROJECT_NAME}
    PUBLIC
      qb::core::noise
      xacc::xacc
      xacc::quantum_gate
      taywee::args
      nlohmann::json
    PRIVATE
      cpr
    INTERFACE
      # The following are not strictly needed, but save dependent packages locating them themselves.
      Eigen3::Eigen
      Python::Python
      pybind11::pybind11
      GTest::gtest
      GTest::gtest_main
   )

  # Install the library
  install(
    TARGETS ${PROJECT_NAME}
    DESTINATION ${CMAKE_INSTALL_PREFIX}/lib
    EXPORT ${PROJECT_NAME}Targets
  )

  # Install the Targets.cmake file for the library
  set(cppTargetsFile "${PROJECT_NAME}Targets.cmake")
  install(
    EXPORT ${PROJECT_NAME}Targets
    FILE ${cppTargetsFile}
    DESTINATION "${CMAKE_INSTALL_PREFIX}/cmake"
    NAMESPACE ${NAMESPACE}::
  )

endif()

# Install the headers
install(
  DIRECTORY include
  DESTINATION ${CMAKE_INSTALL_PREFIX}
)

# Generate the Dependencies.cmake file. 
set(dependenciesFile "coreDependencies.cmake")
set(deps_cmake_file "${CMAKE_CURRENT_BINARY_DIR}/${dependenciesFile}")
file(WRITE ${deps_cmake_file} "# Import all transitive dependencies of qbcore.\n# Auto-generated by cmake. ") 
file(APPEND ${deps_cmake_file} "\nif (NOT TARGET xacc-plugins)\
                                \n  include(${CMAKE_CURRENT_LIST_DIR}/xacc_utilities.cmake)\
                                \nendif()\
                                \nset(XACC_DIR ${XACC_DIR})\
                                \nset(nlohmann_json_DIR ${nlohmann_json_DIR})\
                                \nset(Eigen3_DIR ${Eigen3_DIR})\
                                \nset(args_DIR ${args_DIR})\
                                \nset(pybind11_DIR ${pybind11_DIR})\
                                \nset(GTest_DIR ${GTest_DIR})")
file(APPEND ${deps_cmake_file} "\ncache_install_path()\
                                \n  find_dependency(XACC)\
                                \nreset_install_path()\
                                \nfind_dependency(nlohmann_json ${nlohmann_json_VERSION})\
                                \nfind_dependency(Eigen3 ${Eigen3_VERSION})\
                                \nfind_dependency(args)\
                                \nfind_dependency(pybind11 ${pybind11_VERSION})\
                                \nfind_dependency(Python 3 COMPONENTS Interpreter Development)\
                                \nif(NOT TARGET GTest::gtest)\
                                \n  find_dependency(GTest ${googletest_VERSION})\
                                \nendif()")

# Install the Dependencies.cmake file for the library
install(
  FILES ${CMAKE_CURRENT_BINARY_DIR}/${dependenciesFile}
  DESTINATION "${CMAKE_INSTALL_PREFIX}/cmake"
)
